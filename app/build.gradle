plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'cool.iqbal.demoapptoapp'
    compileSdk rootProject.ext.config['TARGET_SDK']

    defaultConfig {
        applicationId "cool.iqbal.demoapptoapp"
        minSdk rootProject.ext.config['MIN_SDK']
        targetSdk rootProject.ext.config['TARGET_SDK']
        versionCode rootProject.ext.config['VERSION_CODE']
        versionName rootProject.ext.config['VERSION_NAME']

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            def flavor = variant.name
            def versionName = variant.versionName
            def date = new Date()
            def formattedDate = date.format('ddMMyyHHmm')
            outputFileName = "apptoapp_${flavor}_${versionName}_${formattedDate}.apk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    flavorDimensions 'default'
    productFlavors {
        lab {
            dimension 'default'
            buildConfigField "String", "BaseUrl", '"https://learning-cat-saving.ngrok-free.app/"'
            buildConfigField "String", "API_KEY", '"123456789123456789"'
        }
        miniAtm {
            dimension 'default'
            buildConfigField "String", "BaseUrl", '"https://viper.cashlez.com/"'
            buildConfigField "String", "API_KEY", '"123456789123456789"'
        }
        appToApp {
            dimension 'default'
            buildConfigField "String", "BaseUrl", '"https://viper.cashlez.com/"'
            buildConfigField "String", "API_KEY", '"123456789123456789"'
        }
    }

    buildFeatures {
        buildConfig true
    }
}

dependencies {

    implementation project(':jump-app')
    implementation project(':bridge-api')
    implementation rootProject.ext.coreKtx
    implementation rootProject.ext.appCompat
    implementation rootProject.ext.material
    implementation rootProject.ext.constraintLayout
    implementation rootProject.ext.gson
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}